(sys:load "libs/external/glfw3.xtm")
(sys:load "7-variable-density/fluids-7-variable-density.xtm")

(bind-val width i32 800)
(bind-val height i32 600)

(bind-val window GLFWwindow*
  (begin
    (glfwSetErrorCallback (convert (get_native_fptr glfw_error_callback)))
    (glfw_init_and_create_interaction_window width height)))

(sys:load "libs/external/gl/gl-objects.xtm")

;; set up some variables for the OpenGL pipeline

;; this version uses an inverted y axis from the one in gl-objects.xtm
(bind-func VAO_create_ss_quad
  (lambda ()
    (let ((ss_quad_pos_st:float* (salloc 16)))
      (pfill! ss_quad_pos_st
              ;; pos     tex_coord
              -1. -1.    0. 1.
              -1.  1.    0. 0.
              1. -1.    1. 1.
              1.  1.    1. 0.
              )
      (let ((vbo (VBO_create ss_quad_pos_st 16))
            (vao (VAO_create)))
        (VAO_bind_attribute vao vbo 0 2 4 0) ;; position
        (VAO_bind_attribute vao vbo 1 2 4 2) ;; tex_coord
        vao))))

(bind-val tex Texture (Texture_create))
(bind-val vao VAO* (VAO_create_ss_quad))
(bind-val sp ShaderProgram
  (ShaderProgram_create
   (sys_slurp_file "1-matrixless/texture.vert")
   (sys_slurp_file "1-matrixless/texture.frag")))
($ (set! sp (ShaderProgram_create
 (sys_slurp_file "7-variable-density/texture.vert")
 (sys_slurp_file "7-variable-density/texture.frag"))))

(bind-func runsimulation
  (lambda (time:i64 delta_t:double solver:FluidSolver*)
    (let ((densityAir 0.1) (densitySoot .5) (diffusion 0.01) (timestep 0.003)
    (bodies (list (SolidSpere 0.5 0.6 0.1 (* PI .25) 0.0 0.0 0.0))))
    (tset! solver 15 bodies)
    (tset! solver 17 densityAir)
    (tset! solver 18 densitySoot)
    (tset! solver 19 diffusion)
    (addInFlow solver 0.45 0.2 0.1 0.05 1.0 (tref solver 20) 0.0 1.0)
    ;; Fans can be added by compiling in an in flow with no density
    ;    (addInFlow solver 0.1 0.9 0.9 0.1 0.0 0.0 3  .0)
    (updatefs solver timestep))))

(bind-func gl_draw_loop
  (let ((sizex 192)
        (sizey sizex)
        (solver (FluidSolver sizex sizey null 0.0 0.0 0.0))
        (imagedata:float* (alloc (* sizex sizey))))
    (lambda (time:i64 delta_t:double)
      (runsimulation time delta_t solver)
      ;; draw the world
      (glClear GL_COLOR_BUFFER_BIT)
      (let ((texloc (glGetUniformLocation sp "tex")))
        (glUseProgram sp)
        (glUniform1i texloc 0))
      ;; copy the double density data into the float buffer (since OpenGL prefers floats to doubles)
      (doloop (j (* sizex sizey))
        (pset! imagedata j (convert (pref (fsd solver) j)))
        (if (= CELL_SOLID (pref (tref (tref solver 0) 9) j))
            (pset! imagedata j 0.9)))
      (Texture_bind_data tex imagedata sizex sizey 1 GL_CLAMP_TO_EDGE GL_NEAREST)
      (VAO_bind_and_draw_arrays vao GL_TRIANGLE_STRIP 0 4)
      (glfwSwapBuffers window)
      (let ((next_time (+ time (convert (* 44100. delta_t)))))
        (callback next_time gl_draw_loop next_time delta_t)))))

(gl_draw_loop (now) (/ 1. 60.))
